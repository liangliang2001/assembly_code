DATA SEGMENT
ARY DW 7FFFH,0,0FFF6H,8000H,400H,1000H
N EQU ($-ARY)/2
BUF DW 7 DUP(?)
DATA ENDS
STA1 SEGMENT STACK
     DW 40 DUP(?)
STA1 ENDS
CODE SEGMENT
    ASSUME CS:CODE,DS:DATA,SS:STA1
START:
    MOV AX,DATA
    MOV DS,AX
    MOV CX,N
    LEA BX,ARY
REP1: MOV AX,[BX]
    CALL BID
    MOV DL,','
    MOV AH,2
    INT 21H
    ADD BX,2
    LOOP REP1
    MOV AH,4CH
    INT 21H

BID PROC NEAR
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI
    LEA SI,BUF
    OR AX,AX
    JNS PLA1
    NEG AX
    MOV BYTE PTR [SI],'-'
    INC SI
PLA1:
    MOV BX,10
    MOV CX,0
REP2: MOV DX,0
    DIV BX
    PUSH DX
    INC CX
    OR AX,AX
    JNZ REP2
REP3: POP AX
    ADD AL,30H
    MOV [SI],AL
    INC SI
    LOOP REP3
    MOV BYTE PTR [SI],'$'
    LEA DX,BUF
    MOV AH,9
    INT 21H
    POP SI
    POP DX
    POP CX
    POP BX
    RET
BID ENDP
CODE ENDS
END START