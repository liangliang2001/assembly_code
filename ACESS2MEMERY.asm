;允许用户按16进制数的形式输入制定内存单元的段址和偏移，然后用16进制形式显示指定字节单元的内容
CR = 0DH
LF = 0AH
BACKSPACE = 08H
BELLCH = 07H
DATA SEGMENT
SEGOFF DD ?
MESS1 DB 'SEGMENT:$'
MESS2 DB 'OFFSET:$'
BUFFER DB 5 DUP(0)
DATA ENDS
CODE SEGMENT
    ASSUME CS:CODE,DS:DATA
START:
    MOV AX,DATA
    MOV DS,AX

    MOV DX,OFFSET MESS1
    CALL DISPMESS
    CALL GETADR
    MOV WORD PTR SEGOFF+2,AX
    MOV DX,OFFSET MESS2
    CALL DISPMESS
    CALL GETADR
    MOV WORD PTR SEGOFF,AX

    LES DI,SEGOFF
    MOV AL,ES:[DI]
    CALL SHOWAL

    MOV AH,4CH
    INT 21H

SHOWAL PROC
    PUSH AX
    MOV CL,4
    ROL AL,CL
    CALL HTOASC
    CALL PUTCH
    POP AX
    CALL HTOASC
    CALL PUTCH
    RET 
SHOWAL ENDP

GETADR PROC
GETADR1:CALL GETSTR
    CMP BUFFER,CR
    JNZ GETADR2
    CALL BELL
    JMP GETADR1
GETADR2:
    MOV DX,OFFSET BUFFER
    CALL HTOBIN
    CALL NEWLINE
    RET 
GETADR ENDP

GETSTR PROC
    MOV DI,OFFSET BUFFER
    MOV BX,0
GETSTR1:
    CALL GETCH
    CMP AL,CR
    JZ GETSTR5
    CMP AL,BACKSPACE
    JNZ GETSTR4
    CMP CX,0
    JZ GETSTR2
    DEC BX
    CALL PUTCH
    MOV AL,BACKSPACE
    CALL PUTCH
    JMP GETSTR1
GETSTR2:
    CALL BELL
    JMP GETSTR1
GETSTR4:
    CMP BX,4
    JZ GETSTR2
    CALL ISHEX
    JC GETSTR2
    MOV [BX][DI],AL
    INC BX
    CALL PUTCH
    JMP GETSTR1
GETSTR5:
    MOV [BX][DI],AL
    RET 
GETSTR ENDP

HTOBIN PROC
    PUSH CX
    PUSH DX
    PUSH SI
    MOV SI,DX
    XOR DX,DX
    MOV CH,4
    MOV CL,4
HTOBIN1:
    MOV AL,[SI]
    INC SI
    CMP AL,CR
    JZ HTOBIN2
    CALL ATOBIN
    SHL DX,CL
    OR DL,AL
    DEC CH 
    JNZ HTOBIN1
HTOBIN2:
    MOV AX,DX
    POP SI
    POP DX
    POP CX
    RET
HTOBIN ENDP

ISHEX PROC
    CMP AL,'0'
    JB ISHEX2
    CMP AL,'9'+1
    JB ISHEX1
    CMP AL,'A'
    JB ISHEX2
    CMP AL,'F'+1
    JB ISHEX1
    CMP AL,'a'
    JB ISHEX2
    CMP AL,'f'+1
ISHEX1:
    CMC
ISHEX2:
    RET
ISHEX ENDP

ATOBIN PROC
    SUB AL,30H
    CMP AL,9
    JBE ATOBIN1
    SUB AL,7
    CMP AL,15
    JBE ATOBIN1
    SUB AL,20H
ATOBIN1:RET
ATOBIN ENDP

HTOASC PROC
    AND AL,0FH
    CMP AL,9
    JBE HTOASC1
    ADD AL,37H
HTOASC2:RET
HTOASC1:
    ADD AL,30H
    JMP HTOASC2
HTOASC ENDP

PUTCH PROC
    PUSH DX
    MOV DL,AL
    MOV AH,2
    INT 21H
    POP DX
    RET 
PUTCH ENDP

BELL PROC
    MOV AL,BELLCH
    CALL PUTCH
    RET 
BELL ENDP

GETCH PROC
    MOV AH,8
    INT 21H
    RET 
GETCH ENDP

DISPMESS PROC;输出字符串
    MOV AH,9
    INT 21H
    RET 
DISPMESS ENDP

NEWLINE PROC;输出回车换行
    PUSH AX
    PUSH DX
    MOV DL,0DH;输出回车符
    MOV AH,2
    INT 21H
    MOV DL,0AH;输出换行符
    MOV AH,2
    INT 21H
    POP DX
    POP AX
    RET
NEWLINE ENDP
CODE ENDS
END START